# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
# Contributor: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=(rapids-rmm)
_pkgname=rmm
pkgver=0.15.0a.r191.g9f65999
pkgrel=1
pkgdesc="RAPIDS Memory Manager"
arch=('x86_64')
url="https://rapids.ai/"
license=('custom')
depends=('gcc8' 'cython' 'cuda' 'cmake' 'python-numba')
# source=("${_pkgname}::git+https://github.com/rapidsai/rmm.git")
source=("${_pkgname}::git+https://github.com/rapidsai/rmm.git#branch=branch-0.15"
    "setup.py.patch")

sha256sums=('SKIP' 'SKIP')

pkgver() {
    cd rmm

    local _version
    local _revision
    local _shorthash

    _version="$(git tag | sort -Vr | head -n1 | sed 's/^v//')"
    _revision="$(git rev-list v"${_version}"..HEAD --count)"
    _shorthash="$(git rev-parse --short HEAD)"

    printf '%s.r%s.g%s' "$_version" "$_revision" "$_shorthash"
}


build() {
  cd "${srcdir}/rmm"
  git submodule update --init --remote --recursive

  export CC=/usr/bin/gcc-8
  export CXX=/usr/bin/g++-8
  export CUDAHOSTCXX=/usr/bin/g++-8


  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_CXX_FLAGS="-march=skylake -O3" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_TESTS=OFF \
    -DBUILD_BENCHMARKS=OFF \
    -DCMAKE_CXX11_ABI=ON \
    -DGPU_TARGET="sm_75"
  make -j6

  cd "${srcdir}"
  patch -p0 < setup.py.patch

  cd "${srcdir}"/rmm/python
  export LD_LIBRARY_PATH=/usr/lib:/opt/cuda/lib64:/opt/cuda/extras/CUPTI/lib64:/opt/magma/lib
  export CUDA_PATH=/opt/cuda
  export CUDA_HOME=/opt/cuda

  export PATH=/opt/cuda:$PATH
  export CFLAGS='-march=skylake -mtune=native -O3 -pipe -fstack-protector -I/opt/cuda/include'
  export NVCC='/opt/cuda/bin/nvcc'
  export CUB_PATH=/usr
  export LDFLAGS=-L/opt/cuda/lib64

  python setup.py build_ext --inplace --library-dir="${srcdir}/rmm/build"
}

package_rapids-rmm() {
  cd "${srcdir}"/rmm/build
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}"/rmm/python
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1


  install -Dm644 ${srcdir}/rmm/LICENSE ${pkgdir}/usr/share/licenses/rapids-rmm/LICENSE
}

# vim:set ts=2 sw=2 et:
