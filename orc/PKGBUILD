# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=orc
pkgver=0.4.31
pkgrel=2
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://gitlab.freedesktop.org/gstreamer/orc"
arch=(x86_64)
license=(custom)
depends=(glibc)
makedepends=(valgrind git gtk-doc meson)
_commit=9901a96eaff271c2d3b595214213f6805ff803c8  # tags/0.4.31^0
source=("git+https://gitlab.freedesktop.org/gstreamer/orc.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^orc-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}


package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}
